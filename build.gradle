buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://files.minecraftforge.net/maven" }
		mavenLocal()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath('com.gamebuster19901:LicenseChecker'){
			version {
				branch = "master"
			}
		}
	}
}

plugins {
	id 'eclipse'
	id 'maven-publish' 
}
apply plugin: 'net.minecraftforge.gradle'

version = "0.0.0.0-1.14.4"
group = "com.gamebuster19901"
archivesBaseName = "oni"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	mappings channel: "snapshot", version: "20191125-1.14.3"
	// makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
	
	accessTransformer = file('src/main/resources/main/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			property 'forge.logging.console.level', 'debug'

			mods {
				oni {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			property 'forge.logging.console.level', 'debug'

			mods {
				oni {
					source sourceSets.main
				}
			}
		}
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.14.4-28.1.96'
	implementation("com.gamebuster19901:GunCore") {
		version {
			branch = "1.14.4"
		}
	}
}

jar {
	manifest {
		attributes([
			"Specification-Title": "oni",
			"Specification-Vendor": "examplemodsareus",
			"Specification-Version": "1", // We are version 1 of ourselves
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor" :"examplemodsareus",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

task checkLicenses(type: JavaExec) {
	classpath += sourceSets.main.runtimeClasspath
	classpath += buildscript.configurations.classpath
	println main = "com.gamebuster19901.license.LicenseChecker"
	args = ['notBinary', 'silenceSkips', "classes:com.gamebuster19901.license.test.Test"]
}
build.dependsOn ':checkLicenses'

task applyLicenses(type: JavaExec) {
	classpath += sourceSets.main.runtimeClasspath
	classpath += buildscript.configurations.classpath
	println main = "com.gamebuster19901.license.LicenseChecker"
	args = ['notBinary', 'applyLicenses']
}

task editLicenses(type:JavaExec) {
	classpath += sourceSets.main.runtimeClasspath
	classpath += buildscript.configurations.classpath
	standardInput = System.in
	println main = "com.gamebuster19901.license.create.CreateChecker"
	args = ['notBinary', 'applyLicenses']
}

task stripLicenses(type:JavaExec){
	classpath += sourceSets.main.runtimeClasspath
	classpath += buildscript.configurations.classpath
	standardInput = System.in
	println main = "com.gamebuster19901.license.LicenseChecker"
	args = ['notBinary', 'stripLicenses']
}

build.finalizedBy publish

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
	type 'jar'
	builtBy 'reobfJar'
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact reobfArtifact
		}
	}
	repositories {
		mavenLocal()
	}
}